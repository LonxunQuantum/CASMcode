# http://www.scons.org/doc/production/HTML/scons-user.html
# This is: src/casm/SConscript

import os, glob
from glob import glob
from os.path import join

Import('env')

#include path
candidates = [
  env['INCDIR'], 
  env.include_path(env['BOOST_PREFIX'], 'boost')
]
cpppath = [x for x in candidates if x is not None]

# get source files, grouped by compile alias
#   this enables, for example:
#   - using 'scons clex' to compile 'src/casm/clex/*'
#   each file is also aliased, enabling, for example:
#   - using 'scons clex/Configuration.cc' to compile just 'src/casm/clex/Configuration.cc'
casm_lib_src_grp = {
  'clex': ['clex'],
  'app': ['app'],
  'crystallography': ['crystallography'],
  'symmetry': ['symmetry'],
  'basis_set': ['basis_set'],
  'kinetics': ['kinetics'],
  'clusterography': ['clusterography'],
  'database': ['database', 'database/json'],
  'monte_carlo': ['monte_carlo', 'monte_carlo/grand_canonical', 'monte_carlo/canonical'],
  'other': ['casm_io', 'casm_io/json_io', 'container', 'misc', 'strain', 'hull', 'system', 'completer']
}

for name, dirs in casm_lib_src_grp.iteritems():
  files = []
  for x in dirs:
    files += glob(join(x,'*.cc'))
  
  all_sobj = []
  for file in files:
    sobj = env.SharedObject(file, CPPPATH=cpppath)
    env.Alias(file, sobj)
    all_sobj += sobj
  env.Alias(name, all_sobj)
  
  env['COMPILE_TARGETS'] = env['COMPILE_TARGETS'] + all_sobj
  env['CASM_SOBJ'] = env['CASM_SOBJ'] + all_sobj

# other files:
files = ['CASM_global_definitions.cc', 'CASM_global_enum.cc']

for file in files:
  sobj = env.SharedObject(file, CPPPATH=cpppath)
  env.Alias(file, sobj)
  env['COMPILE_TARGETS'] = env['COMPILE_TARGETS'] + sobj
  env['CASM_SOBJ'] = env['CASM_SOBJ'] + sobj

# build version
SConscript(['version/SConscript'], {'env':env})

# qhull c code
include_qhull = join('..','..','include','casm','external','qhull','libqhull_r')
qhull_src = glob( join('external','qhull','libqhull_r','*.c'))
qhull_sobj = env.SharedObject(qhull_src, 
                              CPPPATH=include_qhull)
env['COMPILE_TARGETS'] = env['COMPILE_TARGETS'] + qhull_sobj
env['CASM_SOBJ'] = env['CASM_SOBJ'] + qhull_sobj

# qhull cpp code
include_qhullcpp = join('..','..','include','casm','external','qhull','libqhullcpp')
qhullcpp_src = glob( join('external','qhull','libqhullcpp','*.cpp'))
qhullcpp_sobj = env.SharedObject(qhullcpp_src, 
                                 CPPPATH=[include_qhull, include_qhullcpp])
env['COMPILE_TARGETS'] = env['COMPILE_TARGETS'] + qhullcpp_sobj
env['CASM_SOBJ'] = env['CASM_SOBJ'] + qhullcpp_sobj
